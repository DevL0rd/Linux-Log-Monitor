/*
 * Covers the widget with a centered message when the log collector isn't
 * running (or hasn't written a fresh snapshot). Matches the router monitor's
 * overlay so the two projects feel like one set.
 */
import QtQuick
import QtQuick.Layouts
import org.kde.kirigami as Kirigami
import org.kde.plasma.components as PlasmaComponents

Rectangle {
    id: overlay

    property bool online: true

    z: 100
    visible: !online
    color: Qt.alpha(Kirigami.Theme.backgroundColor, 0.9)
    radius: Kirigami.Units.smallSpacing

    // swallow clicks so the widget underneath can't be interacted with
    MouseArea { anchors.fill: parent }

    ColumnLayout {
        anchors.centerIn: parent
        width: parent.width - Kirigami.Units.largeSpacing * 2
        spacing: Kirigami.Units.smallSpacing

        Kirigami.Icon {
            Layout.alignment: Qt.AlignHCenter
            Layout.preferredWidth: Kirigami.Units.iconSizes.large
            Layout.preferredHeight: Kirigami.Units.iconSizes.large
            source: "dialog-warning"
            opacity: 0.8
        }
        PlasmaComponents.Label {
            Layout.fillWidth: true
            horizontalAlignment: Text.AlignHCenter
            wrapMode: Text.WordWrap
            font.weight: Font.DemiBold
            text: i18n("Log collector not running")
        }
        PlasmaComponents.Label {
            Layout.fillWidth: true
            horizontalAlignment: Text.AlignHCenter
            wrapMode: Text.WordWrap
            opacity: 0.7
            font: Kirigami.Theme.smallFont
            text: i18n("systemctl --user start linux-log-monitor")
        }
    }
}
